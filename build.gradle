/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

import aQute.bnd.build.Workspace

/* Add bnd gradle plugin as a script dependency */
buildscript {
	dependencies {
		classpath bndPlugin
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "1.0.8"
	}

	repositories {
		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}
}

/* Initialize the bnd workspace */
ext.bndWorkspace = Workspace.getWorkspace(rootDir, bnd_cnf)

if (bndWorkspace == null) {
	throw new GradleException("Unable to load workspace ${rootDir}/${bnd_cnf}")
}

bndWorkspace.setProperty('bnd_repourl', bnd_repourl)

ext.cnf = rootProject.project(bnd_cnf)

/* Configure the subprojects */
subprojects {
	repositories {
		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}

	def bndProject = bndWorkspace.getProject(name)

	if (bndProject != null) {
		plugins.apply 'biz.aQute.bnd'
	}

	apply plugin: "com.liferay.source.formatter"

	formatSource {
		baseDirName = "$project.projectDir/src"
	}
}

task wrapper(type: Wrapper) {
	jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}
